#@TYPE: Machine
#@NAME: SolidRun i.MX 8DXL SoM
#@SOC: i.MX8DXL
#@DESCRIPTION: SolidRun i.MX 8DXLite SoM / HummingBoard
#@MAINTAINER: Josua Mayer <josua@solid-run.com>

MACHINEOVERRIDES =. "mx8dxl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa35.inc

IMX_DEFAULT_BSP = "nxp"

MACHINE_FEATURES:remove = " alsa touchscreen"
MACHINE_FEATURES:append = " bluetooth wifi optee"

# Don't include kernels in standard images
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

ATF_PLATFORM = "imx8dxl"
IMX_BOOT_SOC_TARGET = "iMX8DXL"

# This machine is not supported by u-boot-fslc, so we force it to use
# u-boot-imx here.
IMX_DEFAULT_BOOTLOADER = "u-boot-imx"

UBOOT_MAKE_TARGET = "all"
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_SUFFIX = "bin"

# meta layer includes older u-boot-2020, pick 2022.04
PREFERRED_VERSION_u-boot-imx = "2022.04"
PREFERRED_VERSION_u-boot-fslc = "2022.04"

UBOOT_CONFIG ??= "emmc"
UBOOT_CONFIG[emmc] = "imx8dxl_v2x_defconfig,sdcard"

IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS = \
	"${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl', 'flash', d)}"

KERNEL_DEVICETREE:append:use-nxp-bsp = " \
	freescale/imx8dxl-v2x.dtb \
	freescale/imx8dxl-v2x-v11.dtb \
	freescale/imx8dxl-v2x-v11-som-v21.dtb \
"

# TODO: supply board-specific imx-sc-firmware
BOARD_TYPE = "evk"

# generate partitioned emmc disk image
WKS_FILE = "imx8dxl-emmc-data.wks"
IMAGE_FSTYPES:append = " wic wic.bmap"

# don't generate ubifs image (added by imx-v2x distro)
IMAGE_FSTYPES:remove = " ubi"

# include drivers & firmware
# BUG: MACHINE_EXTRA_RDEPENDS should be good enough ...
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append:imx8dxl-sr-som = " \
	kernel-module-authenc \
	kernel-module-brcmfmac \
	kernel-module-brcmutil \
	kernel-module-bridge \
	kernel-module-btbcm \
	kernel-module-caam \
	kernel-module-caam-jr \
	kernel-module-caamalg-desc \
	kernel-module-caamhash-desc \
	kernel-module-caamkeyblob-desc \
	kernel-module-can-dev \
	kernel-module-crct10dif-ce \
	kernel-module-crypto-engine \
	kernel-module-dsa-core \
	kernel-module-dwmac-imx \
	kernel-module-error \
	kernel-module-flexcan \
	kernel-module-fsl-jr-uio \
	kernel-module-fuse \
	kernel-module-g-ether \
	kernel-module-gnss \
	kernel-module-gnss-serial \
	kernel-module-gnss-ubx \
	kernel-module-gpio-pca953x \
	kernel-module-hci-uart \
	kernel-module-imx-sdma \
	kernel-module-libdes \
	kernel-module-nxp-c45-tja11xx \
	kernel-module-nxp-cbtx \
	kernel-module-option \
	kernel-module-pps-gpio \
	kernel-module-rtc-ds1307 \
	kernel-module-sja1105 \
	kernel-module-st-magn \
	kernel-module-st-magn_i2c \
	kernel-module-st-magn_spi \
	kernel-module-st-pressure \
	kernel-module-st-pressure_i2c \
	kernel-module-st-sensors \
	kernel-module-st-sensors_i2c \
	kernel-module-st-sensors_spi \
	kernel-module-stmmac \
	kernel-module-stmmac-platform \
	kernel-module-tag-sja1105 \
	kernel-module-usb-wwan \
	linux-firmware-bcm43455-sr \
	sr-v2x-lte-pwrseq \
	wireless-regdb \
"

# include nice-to-have utilities for peripherals
MACHINE_EXTRA_RDEPENDS:append:imx8dxl-sr-som = " \
	bluez5 \
	can-utils \
	ethtool \
	i2c-tools \
"
