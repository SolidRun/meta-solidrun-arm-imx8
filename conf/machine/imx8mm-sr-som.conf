#@TYPE: Machine
#@NAME: SolidRun i.MX8M Mini SoM
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for SolidRun i.MX8M Mini SoM on HummingBoard Ripple
#@MAINTAINER: Josua Mayer <josua@solid-run.com>

MACHINEOVERRIDES =. "mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc
DEFAULTTUNE:imx8mm-sr-som ?= "cortexa53-crypto"

MACHINE_FEATURES += "wifi bluetooth bcm43455"

# NXP BSP can consume proprietary jailhouse and Broadcom drivers
# OP-TEE is also applicable to NXP BSP, mainline BSP seems not to have
# a full support for it yet.
MACHINE_FEATURES:append:use-nxp-bsp = " jailhouse optee"

KERNEL_DEVICETREE = " \
	freescale/imx8mm-hummingboard-ripple.dtb \
"
# HACK: place copy of kernel dtb in vendor subfolder as expected by u-boot
IMAGE_BOOT_FILES:append = " imx8mm-hummingboard-ripple.dtb;freescale/imx8mm-hummingboard-ripple.dtb "

UBOOT_DTB_NAME = "imx8mm-hummingboard-ripple.dtb"

IMX_DEFAULT_BOOTLOADER:use-nxp-bsp = "u-boot-imx"
IMX_DEFAULT_BOOTLOADER:use-mainline-bsp = "u-boot-fslc"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]      = "imx8mm_sr_som_defconfig,sdcard"

SPL_BINARY = "spl/u-boot-spl.bin"

ATF_PLATFORM = "imx8mm"
ATF_LOAD_ADDR = "0x960000"

IMXBOOT_TARGETS = "flash_evk"

IMX_BOOT_SOC_TARGET = "iMX8MM"

SERIAL_CONSOLES = "115200;ttymxc1"

BOOT_SPACE = "65536"
LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = "all"

IMX_BOOT_SEEK = "33"

OPTEE_BIN_EXT = "8mm"
TEE_LOAD_ADDR = "0xbe000000"

DDR_FIRMWARE_NAME = " \
    lpddr4_pmu_train_1d_imem.bin \
    lpddr4_pmu_train_1d_dmem.bin \
    lpddr4_pmu_train_2d_imem.bin \
    lpddr4_pmu_train_2d_dmem.bin \
"

# use modified kickstart file with generic distro-boot
WKS_FILE = "imx-boot-bootpart-distro.wks"

# Add additional firmware
MACHINE_FIRMWARE:append = " linux-firmware-bcm43455"

# include drivers & support packages
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = " \
    kernel-module-af-alg \
    kernel-module-algif-hash \
    kernel-module-algif-skcipher \
    kernel-module-authenc \
    kernel-module-brcmfmac \
    kernel-module-brcmutil \
    kernel-module-btbcm \
    kernel-module-caam \
    kernel-module-caamalg-desc \
    kernel-module-caamhash-desc \
    kernel-module-caam-jr \
    kernel-module-caamkeyblob-desc \
    kernel-module-cfg80211 \
    kernel-module-crct10dif-ce \
    kernel-module-crypto-engine \
    kernel-module-error \
    kernel-module-fsl-jr-uio \
    kernel-module-fuse \
    kernel-module-hci-uart \
    kernel-module-imx-sdma \
    kernel-module-libdes \
    kernel-module-overlay \
    kernel-module-polyval-ce \
    kernel-module-polyval-generic \
    kernel-module-rfkill \
    kernel-module-rfkill-gpio \
    kernel-module-rtc-abx80x \
    kernel-module-secvio \
    ${MACHINE_FIRMWARE} \
"
# builtin:
# kernel-module-fec

# add non-essential packages
MACHINE_EXTRA_RRECOMMENDS = ""
