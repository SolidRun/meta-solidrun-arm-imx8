#@TYPE: Machine
#@NAME: SolidRun i.MX8M Plus SoM
#@SOC: i.MX8MP
#@DESCRIPTION: Machine configuration for SolidRun i.MX8M Plus SoM
#@MAINTAINER: Josua Mayer <josua@solid-run.com>

MACHINEOVERRIDES =. "mx8mp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc
DEFAULTTUNE ?= "cortexa53-crypto"

MACHINE_FEATURES += "pci wifi bluetooth bcm43455"

# NXP BSP can consume proprietary jailhouse
# OP-TEE is also applicable to NXP BSP, mainline BSP seems not to have
# a full support for it yet.
MACHINE_FEATURES:append:use-nxp-bsp = " optee jailhouse"

KERNEL_DEVICETREE = " \
	freescale/imx8mp-cubox-m.dtb \
"
# HACK: place copy of kernel dtb in vendor subfolder as expected by u-boot
IMAGE_BOOT_FILES:append = " imx8mp-cubox-m.dtb;freescale/imx8mp-cubox-m.dtb "

UBOOT_DTB_NAME = "imx8mp-cubox-m.dtb"

IMX_DEFAULT_BOOTLOADER = "u-boot-imx"

UBOOT_SUFFIX = "bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mp_solidrun_defconfig"

SPL_BINARY = "spl/u-boot-spl.bin"

ATF_PLATFORM = "imx8mp"

IMXBOOT_TARGETS = "flash_evk"

IMX_BOOT_SOC_TARGET = "iMX8MP"

SERIAL_CONSOLES = "115200;ttymxc1"

LOADADDR = ""
UBOOT_MAKE_TARGET = "all"
IMX_BOOT_SEEK = "32"

OPTEE_BIN_EXT = "8mp"
TEE_LOAD_ADDR = "0x56000000"

# Set DDR FIRMWARE
DDR_FIRMWARE_VERSION = "202006"
DDR_FIRMWARE_NAME = " \
    lpddr4_pmu_train_1d_dmem_${DDR_FIRMWARE_VERSION}.bin \
    lpddr4_pmu_train_1d_imem_${DDR_FIRMWARE_VERSION}.bin \
    lpddr4_pmu_train_2d_dmem_${DDR_FIRMWARE_VERSION}.bin \
    lpddr4_pmu_train_2d_imem_${DDR_FIRMWARE_VERSION}.bin \
"

# use modified kickstart file with generic distro-boot
WKS_FILE = "imx-boot-bootpart-distro.wks.in"

# Add additional firmware
MACHINE_FIRMWARE:append = " linux-firmware-bcm43455"

# include drivers & support packages
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = " \
    kernel-module-af-alg \
    kernel-module-algif-hash \
    kernel-module-algif-skcipher \
    kernel-module-at24 \
    kernel-module-authenc \
    kernel-module-brcmfmac \
    kernel-module-brcmfmac-wcc \
    kernel-module-brcmutil \
    kernel-module-caam \
    kernel-module-caamalg-desc \
    kernel-module-caamhash-desc \
    kernel-module-caam-jr \
    kernel-module-caamkeyblob-desc \
    kernel-module-crct10dif-ce \
    kernel-module-crypto-engine \
    kernel-module-dw-hdmi-cec \
    kernel-module-error \
    kernel-module-fsl-jr-uio \
    kernel-module-fuse \
    kernel-module-gpio-ir-recv \
    kernel-module-imx-sdma \
    kernel-module-libdes \
    kernel-module-overlay \
    kernel-module-polyval-ce \
    kernel-module-polyval-generic \
    kernel-module-rc-core \
    kernel-module-rtc-ds1307 \
    kernel-module-secvio \
    kernel-module-snd-soc-fsl-aud2htx \
    kernel-module-snd-soc-imx-hdmi \
    ${MACHINE_FIRMWARE} \
"
# builtin:
# kernel-module-btbcm
# kernel-module-cfg80211
# kernel-module-fec
# kernel-module-hci-uart

# add non-essential packages
MACHINE_EXTRA_RRECOMMENDS = ""

IMX_DEFAULT_BSP = "nxp"
